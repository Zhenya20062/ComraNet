plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.devtools.ksp' version '1.7.0-1.0.6'
    id "dagger.hilt.android.plugin"
}

android {
    compileSdk 33

    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }

    defaultConfig {
        applicationId "com.euzhene.comranet"
        minSdk 25
        targetSdk 33
        versionCode 1
        versionName "0.6.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        release {
            keyAlias properties.getProperty('keyAlias')
            keyPassword properties.getProperty('keyPassword')
            storeFile file(properties.getProperty('storeFile'))
            storePassword properties.getProperty('storePassword')
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.2.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.euzhene.comranet'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //<------------------compose------------------->
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.activity:activity-compose:1.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    //navigation
    implementation 'io.github.raamcosta.compose-destinations:core:1.6.27-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.6.27-beta'
    implementation 'io.github.raamcosta.compose-destinations:animations-core:1.6.27-beta'

    //<-----------------firebase-------------------->
    implementation platform('com.google.firebase:firebase-bom:29.1.0')
    //storage
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    //authentication
    implementation 'com.firebaseui:firebase-ui-auth:7.2.0'


    //<-----------------coroutines-------------------->
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    //<-------------------paging-------------------->
    implementation "androidx.paging:paging-runtime:3.1.1"
    implementation("androidx.paging:paging-compose:1.0.0-alpha17")

    //<-----------------room-------------------->
    def room_version = "2.5.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    //datastore
    implementation("androidx.datastore:datastore-preferences:1.0.0")

    //<-----------------hilt-------------------->
    implementation "com.google.dagger:hilt-android:2.43.2"
    kapt "com.google.dagger:hilt-android-compiler:2.43.2"
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    //coil
    implementation("io.coil-kt:coil-compose:2.1.0")

    //color picker
    implementation 'com.godaddy.android.colorpicker:compose-color-picker:0.4.2'

    //gson
    implementation 'com.google.code.gson:gson:2.9.0'
    //onesignal(notifications)
    implementation 'com.onesignal:OneSignal:[4.0.0, 4.99.99]'

    //accompanist
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.28.0"

    //splash screen
    implementation 'androidx.core:core-splashscreen:1.0.0'

}
kapt {
    correctErrorTypes = true
}